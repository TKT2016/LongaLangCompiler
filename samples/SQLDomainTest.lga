//SQLDomainTest.lga
package samples;

import chain longa.domains.sql.connection_chain;
import chain longa.domains.sql.insert_chain;
import chain longa.domains.sql.select_chain;
import chain longa.domains.sql.create_chain;
import chain longa.domains.sql.column_chain;
import chain longa.domains.sql.drop_chain;

import longa.domains.sql.tools.DbDataHelper;
import longa.domains.sql.*;
import longa.domains.sql.models.*;
import static longa.domains.sql.models.JoinKind.*;
import static longa.domains.sql.models.DataTypeKind.*;

import com.sun.rowset.CachedRowSetImpl;

class SQLDomainTest {

    String tbWebSite="websites";
    String tbstudent ="student";
    String tbcourse ="course";

     void main() {
         System.out.println("begin access mysql");
         // connection mysql
         connection "localhost" port 3306 use "testdb" user "root" password "123456" ;

         /* error call connections*/
         // connection "localhost" port 3306 use "testdb" user "root"  ; //invoke chain need call 'password'
         // connection "localhost" port 3306 use "testdb" user "root"  user "root2" password "123456" ; // invoke chain call 'user' only once

         newLine();

         sql_table_WebSite();

         newLine();

         sql_table_student();

         newLine();

         sql_table_course();

         newLine();

         sql_join();
     }

     void sql_join()
     {
         //select join sql
         CachedRowSetImpl rs1 =
         select "A.*,B.*,C.*"
         from tbstudent "A"
             left join tbWebSite "B" on "A.pageid = B.id"
             left join tbcourse "C" on "A.courseid = C.id"
         where "B.alexa > 1"
         orderby  "A.name";

         DbDataHelper.dump(System.out,rs1);
     }

     void sql_table_WebSite()
     {
         //  drop table
         drop table tbWebSite ifExists;
         // create table
         create table tbWebSite [
             column "id" dataType integer NotNULL ,
             column "name" dataType varchar 255 NotNULL comment "站点名称",
             column "url" dataType varchar 255 NotNULL,
             column "alexa" dataType integer NULL  comment "Alexa 排名",
             column "country"dataType varchar 255 NotNULL comment "国家",
         ];

         //insert
         insert into tbWebSite values ["1", "Google", "https://www.google.cm/", "1", "USA"] ;
         insert into tbWebSite values ["2", "淘宝", "https://www.taobao.com/", "13", "CN"];
         insert into tbWebSite values ["3", "微博", "http://weibo.com/", "20", "CN"];
         insert into tbWebSite values ["4", "Facebook", "https://www.facebook.com/", "3", "USA"];

         CachedRowSetImpl rs2 = select "*" from tbWebSite;

         DbDataHelper.dump(System.out,rs2);
     }

    void sql_table_student()
    {
        //  drop table
        drop table tbstudent ifExists;
        // create table
        create table tbstudent [
            column "no" dataType integer 11 NotNULL  ,
            column "name"  dataType CHAR 8  NotNULL,
            column "sex" dataType integer ,
            column "birthday" dataType DATETIME,
            column "pageid" dataType integer,
            column "courseid" dataType integer
        ];
        //insert
        insert into tbstudent values[1,"曾华",1,"1977-09-01",1,1];
        insert into tbstudent values[2,"匡明",1,"1975-10-02",2,3];
        insert into tbstudent values[3,"王丽",0,"1976-01-23",3,2];
        insert into tbstudent values[4,"李军",1,"1976-02-20",4,1];
        insert into tbstudent values[5,"王芳",0,"1975-02-10",1,3];
        insert into tbstudent values[6,"陆君",1,"1974-06-03",2,2];

        CachedRowSetImpl rs2 = select "*" from tbstudent;

        DbDataHelper.dump(System.out,rs2);
    }

    void sql_table_course()
    {
        // drop table
        drop table tbcourse ifExists;

        // create table
        create table tbcourse [
            column "id" dataType integer 11 NotNULL  ,
            column "name"  dataType CHAR 8  NotNULL
        ];
        //insert
        insert into tbcourse values[1,"语文"];
        insert into tbcourse values[2,"数学"];
        insert into tbcourse values[3,"外语"];

        CachedRowSetImpl rs3 = select "*" from tbcourse;

        DbDataHelper.dump(System.out,rs3);
    }

    void newLine()
    {
        System.out.println();
    }
}
